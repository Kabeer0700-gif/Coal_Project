INCLUDE IRVINE32.inc


.data 
    
  ;--------------------- prompt/data for user input -----------------------

    prompt      BYTE 13,10,"Enter plaintext (max 255 chars) =====>  ",0
    welcomeMsg BYTE "Welcome to Advanced Encryption/Decryption Program",0
	text byte 256 DUP (?)  ; input ismein store hoga jo user dalega
	bufSize     = 256
    encTEXT byte " Encrypted string ====>>: ",0
	decTEXT byte " Decrypted string ====>>: ",0
    exitMsg BYTE "------------ Thanks For Using Our Encryption Decryption Program --------------",0
    len DWORD 0
	opt BYTE 0 
	choice BYTE ? ; choice for algorithm

 ;------------------------------------------------------------------------

 ;---------------------- Output data -----------------------------------

    cipher      BYTE bufSize DUP(0) ; encrypted msg ismein aaaiga
	decrypted   BYTE bufSize DUP(0) ; decrypted msg ismein aaiga
   

 ;----------------------------------------------------------------------

 ;---------------------- Ceaser Cipher Data -------------------------

    keyPrompt    BYTE   "Enter key (1 to 25 => press Enter for default 3): ",0
    encryptionKEY BYTE 3 
;--------------------------------------------------------------------


;------------------------- XOR cipher Data --------------------------

    key BYTE ? ; key for xor cipher
	keyXORMSG BYTE "ENTER KEY : ",0

;------------------------------------------------------------------
     
 ;------------------------- Substitution Data --------------------------

	subTable byte "QWERTYUIOPASDFGHJKLZXCVBNM",0
	
 ;----------------------------------------------------------------------

 ;-------------------------     OTP Data --------------------------

    keyOTP         BYTE bufSize DUP(0) ; key of OTP cipher
	outCipher   BYTE "Ciphertext (hex): ",0
	outDec      BYTE 13,10,"Decrypted: ",0
	outKey      BYTE 13,10,"Key (hex): ",0

 ;-----------------------------------------------------------------
    msgLen     DWORD ?

 ;--------------------------- decor stuff --------------------------
	
    summaryBoxStar BYTE 13,10,
"**********************************************",13,10,\
"**                 SUMMARY                 **",13,10,\
"**********************************************",13,10,13,10,0
	MSG_SUMMARY BYTE 13,10," [Summary] Encryption Completed Successfully ",13,10,0
	MSG_ALGO BYTE " Algorithm ====>> ",0
	MSG_KEY_USED BYTE " Key Used  ====>> ",0
	MSG_KEY_GENERATED BYTE " A random OTP key was generated.",0
	algoNameXOR BYTE "XOR Cipher",0
	algoNameCaesar BYTE "Caesar Cipher",0
	algoNameSub BYTE "Substitution Cipher",0
	algoNameOTP BYTE "One-Time Pad (OTP)",0

 footer BYTE   13,10,"**********************************************",13,10,0

 FreqMSG BYTE 13,10,
"**********************************************",13,10,\
"**                Frequency Analysis           **",13,10,\
"**********************************************",13,10,13,10,0

  menuTop     BYTE "  .================================================.  ",0
menuTitle   BYTE " /     ADVANCED ENCRYPTION SUITE                   \\ ",0
 menuMid     BYTE " |===================================================|",0
 menu1       BYTE " |  > [1] Encrypt Message                            |",13,10,0
 menu2       BYTE " |  > [2] Decrypt Message                            |",13,10,0
 menu3       BYTE " |  > [3] File Encryption Mode                       |",13,10,0
 menu4       BYTE " |  > [4] Frequency Analysis                         |",13,10,0
 menu5       BYTE " |  > [5] Exit                                       |",13,10,0
menuBottom  BYTE " \\================================================/ ",0

optMSGTop    BYTE "  ---------------- ",13,10,0
optMSGBody   BYTE " | Select Option: |  ",0
optMSGBottom BYTE 13,10,"  ----------------           ",0

otpDecor BYTE "====>", 0

algo1 BYTE \
"###############################################",13,10,\
"#              Choose Algorithm                #",13,10,\
"###############################################",13,10,\
"# > [1] Caesar Cipher                          #",13,10,\
"# > [2] XOR Cipher                             #",13,10,\
"# > [3] Substitution Cipher                    #",13,10,\
"# > [4] One-Time Pad (Random)                  #",13,10,\
"###############################################",13,10,0



choiceMSGTop    BYTE "  ---------------- ",13,10,0
choiceMSGBody   BYTE "| Choose Algorithm |  ",0
choiceMSGBottom BYTE 13,10,"  ----------------           ",0
choiceDecor BYTE "====>", 0



 ;------------------------------------------------------------------
	

 ;------------------------- File handling Data --------------------

  inFileName   BYTE 80 DUP(0)
outFileName  BYTE 80 DUP(0)

fileHandle   DWORD ?

fileReadMsg  BYTE "Enter input file name: ",0
fileWriteMsg BYTE "Enter output file name: ",0

fileErrOpen  BYTE "Error: Cannot open input file!",0
fileErrRead  BYTE "Error: Unable to read file!",0
fileSaved    BYTE "Output saved to file successfully.",0
FOF BYTE 1 ; check if files open fails or not
FRF BYTE 1 ; check if files read fails or not


 ;-----------------------------------------------------------------
	freq DWORD 26 DUP(0)    ; A to Z counter


 ;--------------------------- Strengtg Meter DatA --------------------

	smWeak      BYTE " Strength ====>> Weak",0
	smMedium    BYTE " Strength ====>> Medium",0
	smStrong    BYTE " Strength ====>> Strong",0
	seen256     BYTE 256 DUP(0)       ; used for key and message uniqueness

  ;-----------------------------------------------------------------


    
  ;-------------------------------- Error Handling Data -----------------------
	
    isEncrypted BYTE 0      ; 0 = no encrypted data available, 1 = encrypted data stored

	MSG_NO_CIPHER BYTE "Error: No encrypted data available! Please encrypt first.",0

	MSG_ALGO_MISMATCH BYTE "Error: You are attempting to decrypt using the wrong algorithm!",0

	;REMEMBERING WHICH ALGO WAS USED WHILE ENCRYPTING
	
	lastAlgo BYTE 0  ; 0 = none  1 = Caesar  2 = XOR  3 = Substitution   4 = OTP
	lastCaesarKey BYTE 0
	lastXORKey BYTE 0

    
  ;--------------------------------------------------------------------------
	
	


	





	


 
spaceChar BYTE ' '



keyMSG BYTE "Generating random key... ",0
MSG2   BYTE "Encrypting... ",0
MSG3   BYTE "? Encryption Successful! ",0
MSG4   BYTE "Ciphertext: ",0







EXTERN SetConsoleOutputCP@4 : PROC
EXTERN SetConsoleCP@4 : PROC

.code

;-------------------- MENU ---------------------

IntroAnimation PROC
   
    mov esi, OFFSET welcomeMsg
    xor ecx, ecx

lenLoop:
    cmp byte ptr [esi+ecx], 0
    je lenDone
    inc ecx
    jmp lenLoop

lenDone:
    mov msgLen, ecx

    
    ; Compute column = (80 - msgLen) / 2
    mov eax, 80
    sub eax, msgLen
    shr eax, 1            ; divide by 2
    mov ebx, eax          ; EBX = column (x)

    mov ecx, 12           ; row (y) = middle of screen
    call Gotoxy

    
    mov esi, OFFSET welcomeMsg
    xor edx, edx          ; index = 0

printLoop:
    cmp edx, msgLen
    je doneTyping
    mov al, [esi+edx]
    call WriteChar
    inc edx

    call TypeDelay
    jmp printLoop

doneTyping:
    ; Wait 5 seconds then clear screen
    call HoldScreen
    call Clrscr
    ret
IntroAnimation ENDP



TypeDelay PROC
    mov eax, 50
    call Delay
    ret
TypeDelay ENDP

HoldScreen PROC
    mov eax, 5000
    call Delay
    ret
HoldScreen ENDP


WriteCentered PROC
    push edx
    mov eax, 80          ; console width
    mov ecx, 0           ; string length counter
    mov esi, edx         ; save string address
countLoop:
    cmp byte ptr [esi+ecx],0
    je countDone
    inc ecx
    jmp countLoop
countDone:
    sub eax, ecx          ; remaining space
    shr eax, 1            ; half on left
    mov edx, eax
    call WriteSpaces
    pop edx
    call WriteString
    ret
WriteCentered ENDP

; Print spaces
WriteSpaces PROC
    mov ecx, edx
    mov edx, OFFSET spaceChar
spaceLoop:
    cmp ecx,0
    je spaceDone
    mov al,' '
    call WriteChar
    dec ecx
    jmp spaceLoop
spaceDone:
    ret
WriteSpaces ENDP

PrintMENU PROC

    mov edx, OFFSET menuTop
    call WriteCentered
    call CrLf

    mov edx, OFFSET menuTitle
    call WriteCentered
    call CrLf

    mov edx, OFFSET menuMid
    call WriteCentered
    call CrLf

    mov edx, OFFSET menu1
    call WriteCentered
    call CrLf

    mov edx, OFFSET menu2
    call WriteCentered
    call CrLf

    mov edx, OFFSET menu3
    call WriteCentered
    call CrLf

    mov edx, OFFSET menu4
    call WriteCentered
    call CrLf

    mov edx, OFFSET menu5
    call WriteCentered
    call CrLf

    mov edx, OFFSET menuBottom
    call WriteCentered
    call CrLf

    ret
PrintMENU ENDP



;-------------------- INPUT ---------------------

takeInput PROC
     
	mov edx,offset text
	mov ecx,sizeOF text
	call readString
	call crlf
    ret

takeInput ENDP

;-------------------- Print Algorithm ----------------------

printAlgo PROC

    mov edx,offset algo1
	call writeString
    call crlf
    call crlf
    mov edx,offset choiceMSGTOP 
	call writeString
    mov edx,offset choiceMSGBody 
	call writeString
    mov edx,offset choiceDecor
    call writeString
    mov edx,offset choiceMSGBottom 
	call writeString

    ret
printAlgo ENDP

;--------------------  ENCRYPT CEASER ----------------------
GetEncryptionKey PROC
    mov edx, OFFSET keyPrompt
    call WriteString
    call ReadInt
    jz useDefaultKey

    cmp eax, 1
    jl useDefaultKey

    cmp eax, 25
    jg useDefaultKey

    mov encryptionKey, al
    ret
useDefaultKey:
    mov encryptionKey, 3
    ret
GetEncryptionKey ENDP

EncryptCaesar PROC
	
	call getEncryptionKEY
   
	mov ecx,len
    mov esi, OFFSET text
    mov edi, OFFSET cipher
encrypt_loop:
    mov al, [esi]
    cmp al, 'A'
    jb not_uppercase

    cmp al, 'Z'
    ja check_lowercase
    add al, encryptionKey

    cmp al, 'Z'
    jbe store_encrypted

    sub al, 26
    jmp store_encrypted
check_lowercase:
    cmp al, 'a'
    jb not_lowercase

    cmp al, 'z'
    ja not_lowercase

    add al, encryptionKey
    cmp al, 'z'

    jbe store_encrypted
    sub al, 26

    jmp store_encrypted
not_lowercase:
not_uppercase:
store_encrypted:
    mov [edi], al
    inc esi
    inc edi
    loop encrypt_loop
    mov BYTE PTR [edi], 0

	call crlf
    mov isEncrypted, 1
	mov lastAlgo, 1
	mov bl,encryptionKey
	mov lastCaesarKey, bl
	
    call ResultEnc
  ret 
EncryptCaesar ENDP

;------------------ DECRYPT CEASER -----------------------

DecryptCaesar PROC
    mov ecx, [len]
    mov esi, OFFSET cipher
    mov edi, OFFSET decrypted
	mov al, lastCaesarKey
    mov encryptionKey, al

decrypt_loop:
    mov al, [esi]
    cmp al, 'A'
    jb decrypt_not_uppercase

    cmp al, 'Z'
    ja decrypt_check_lowercase
    sub al, encryptionKey

    cmp al, 'A'
    jae decrypt_store_decrypted

    add al, 26
    jmp decrypt_store_decrypted
decrypt_check_lowercase:
    cmp al, 'a'
    jb decrypt_not_lowercase
    cmp al, 'z'

    ja decrypt_not_lowercase
    sub al, encryptionKey

    cmp al, 'a'
    jae decrypt_store_decrypted

    add al, 26
    jmp decrypt_store_decrypted

decrypt_not_lowercase:
decrypt_not_uppercase:
decrypt_store_decrypted:
    mov [edi], al
    inc esi
    inc edi
    loop decrypt_loop
    mov BYTE PTR [edi], 0
    call ResultDec
	
    ret
DecryptCaesar ENDP
;--------------------   ENCRYPT XOR  ------------------------

EncryptXor PROC
	mov edx,offset keyXORMSG
	call writeString
	call readINT
	mov key,al

	mov esi, offset text

	mov edi, offset cipher
	
	mov ecx, [len]

	L1:
		mov al, [esi]
		xor al, key
		mov [edi], al
		inc esi
		inc edi
		loop L1
	
	mov  byte ptr [edi], 0  ; did this to null terminate the string

	

	call crlf
	mov isEncrypted, 1
	mov lastAlgo, 2
	mov bl,key
	mov lastXORKey, bl
	call ResultEnc
	ret

	EncryptXor ENDP


;--------------------  DECRYPT XOR ----------------------

DecryptXor PROC
	
	mov esi, offset cipher
	mov edi, offset decrypted
	
	mov ecx, [len]             
	mov al, lastXORKey
	mov key, al

	L2:
		mov al, [esi]
		xor al, key
		mov [edi], al
		inc esi
		inc edi
		loop L2
	
	mov  byte ptr [edi], 0


	call resultDec

	ret

	DecryptXor ENDP



;-------------------- ENCRYPT SUBSTITUION ---------------------

encryptSub PROC
	
	push ebp
	mov ebp,esp
	mov ecx,[ebp+8]
	mov ebx,0
	mov esi,offset text
	mov edi,offset cipher

encrypt:

	mov al,[esi]
	cmp al,'A'
	jb notLetter
	cmp al,'Z'
	jbe UpperCase
	
	cmp al,'a'
	jb notLetter
	cmp al,'z'
	jbe LowerCase
	
notLetter:
	mov [edi],al
	jmp nextChar

UpperCase:
	sub al,'A' 
	movzx ebx,al
	mov al,subTable[ebx]
	jmp storeLetter

LowerCase:
	sub al,'a'
	movzx ebx,al
	mov al,subTable[ebx]
	add al,32
	jmp storeLetter
	

storeLetter:
	mov [edi],al

nextChar:
	inc esi
	inc edi

Loop encrypt
	
	mov byte ptr [edi],0
	
	pop ebp
	mov isEncrypted, 1
	mov lastAlgo, 3
	call ResultEnc
		ret 4

encryptSub ENDP

;-------------------- DECRYPT SUBSTITUION ---------------------

decryptSub PROC

mov esi,offset cipher
mov edi,offset decrypted
mov ecx,[len]
mov eax,0


DECRYPT:
	mov al,[esi]
	cmp al,'A'
	jb notLetter
	cmp al,'Z'
	jbe upperCase

	mov al,[esi]
	cmp al,'a'
	jb notLetter
	cmp al,'z'
	jbe lowerCase

notLetter:
	mov [edi],al
	jmp nextChar

UpperCASE:
	mov bl,0
findUpper:
	mov dl,subTable[ebx]
	cmp al,dl
	je FoundUpper
	inc bl
	cmp bl,26
	jb findUpper
	jmp nextchar
FoundUpper:
	mov al,bl
	add al,'A'
	jmp storeValue

LowerCase:
	sub al,32
	mov bl,0

findLower:
	mov dl,subTable[ebx]
	cmp al,dl
	je foundLower
	inc bl
	cmp bl,26
	jb findLower
	jmp nextChar

foundLower:
	mov al,bl
	add al,'a'
	jmp storeValue
	
storeValue:	
	mov [edi],al

nextChar:
	inc esi
	inc edi

Loop DECRYPT

	mov byte ptr [edi],0
    call resultDec
	ret
decryptSub ENDP	

; ---------------- ENCRYPT OTP ---------------------
encryptOTP PROC
    push ebp
    mov ebp, esp
    mov ecx, [ebp+8]      
    mov [len], ecx        
    call randomize

    xor esi, esi          ; index = 0
    mov ecx, [len]        

gen_KEY:
    mov eax, 256
    call randomRange    
    mov [keyOTP + esi], al   

    mov al, [text + esi]  
    mov bl, [keyOTP + esi]
    xor al, bl
    mov [cipher + esi], al

    inc esi
    loop gen_KEY

 
    mov edx, OFFSET outCipher
    call WriteString

  
    xor esi, esi
    mov ecx, [len]

	mov edx,offset MSG4
	call crlf
	call writeString
	

print_cipher:
    mov al, [cipher + esi]    ; byte to print
    call WriteHexB            
    mov al, ' '
    call WriteChar
    inc esi
    loop print_cipher

    call Crlf

	mov edx, OFFSET outKey
    call WriteString

    xor esi, esi
    mov ecx, [len]

print_key:
    mov al, [keyOTP + esi]
    mov ebx, 1
    call WriteHexB
    mov al, ' '
    call WriteChar

    inc esi
    loop print_key

    call Crlf
	call crlf

    pop ebp
	mov isEncrypted, 1
	mov lastAlgo, 4
	
    mov edx,offset summaryBoxStar 
    call writeString
     call PrintSummary
    call StrengthMeter
    mov edx,offset footer
    call writeString
    call crlf
    ret 4

encryptOTP ENDP


; ---------------- DECRYPT OTP ---------------------
decryptOTP PROC
    push ebp
    mov ebp, esp
    mov ecx, [len]        ; get length
    xor esi, esi          ; index = 0

dec_loop:
    mov al, [cipher + esi]
    mov bl, [keyOTP + esi]
    xor al, bl
    mov [decrypted + esi], al
    inc esi
    loop dec_loop

    ; null-terminate decrypted string
    mov eax, [len]
    mov byte ptr [decrypted + eax], 0

    mov edx, OFFSET outDec
    call WriteString
    mov edx, OFFSET decrypted
    call WriteString
    call Crlf
    call crlf
    pop ebp
    ret
decryptOTP ENDP


PrintSummary PROC
	mov edx, OFFSET MSG_SUMMARY
	call WriteString

	mov edx, OFFSET MSG_ALGO
	call WriteString

	; Print which algo was used
	cmp lastAlgo, 1
	je p_caesar
	cmp lastAlgo, 2
	je p_xor
	cmp lastAlgo, 3
	je p_sub
	cmp lastAlgo, 4
	je p_otp
	jmp donePS

p_caesar:
    mov edx, OFFSET algoNameCaesar
    call WriteString
    call crlf

    mov edx, OFFSET MSG_KEY_USED
    call WriteString
    movzx eax, lastCaesarKey
    call WriteDec
    jmp donePS


p_xor:
    mov edx, OFFSET algoNameXOR
    call WriteString
    call crlf

    mov edx, OFFSET MSG_KEY_USED
    call WriteString
    movzx eax, lastXORKey
    call WriteDec
    jmp donePS

p_sub:
    mov edx, OFFSET algoNameSub
    call WriteString
    jmp donePS


p_otp:
    mov edx, OFFSET algoNameOTP
    call WriteString
    call crlf

    mov edx, OFFSET MSG_KEY_GENERATED
    call WriteString
    jmp donePS


donePS:
	call crlf
	ret
PrintSummary ENDP


; ---------------- Frequency Analysis ---------------------
FrequencyAnalysis PROC
    mov al,isEncrypted
    cmp al,0
    je FreqERR
    mov edi, offset freq
    mov ecx, 26
    mov eax,0
FA_zero:
    mov [edi], eax
    add edi, 4
    loop FA_zero

    mov esi, OFFSET cipher
    mov ecx, [len]
FA_scan:
    mov al, [esi]

    cmp al, 'A'
    jb  FA_check_lower
    cmp al, 'Z'
    ja  FA_check_lower
    sub al, 'A'
    movzx edx, al
    inc DWORD PTR [freq + edx*4]
    jmp FA_next

FA_check_lower:
    ; lowercase a..z
    cmp al, 'a'
    jb  FA_next
    cmp al, 'z'
    ja  FA_next
    sub al, 'a'
    movzx edx, al
    inc DWORD PTR [freq + edx*4]

FA_next:
    inc esi
    loop FA_scan



    mov ecx, 26
    xor edi, edi
    mov edx,offset freqMSG
    call writeString
   
FA_print:
    mov eax, edi
    add al, 'A'
    call WriteChar
    mov al, '>'
    call WriteChar
    mov al, ' '
    call WriteChar
    mov eax, [freq + edi*4]
    call WriteDec
    call Crlf
    inc edi
    loop FA_print
    mov edx,offset footer
    call writeSTRING
    call crlf
    jmp done
 FreqERR:
    mov edx,offset msg_no_cipher
    call writeString
    call crlf
    call crlf
    

    Done:
    ret
FrequencyAnalysis ENDP



; ---------------- Encryption Strength Meter ---------------------

StrengthMeter PROC
    pushad

    xor eax, eax                  ; eax = algScore
    xor ebx, ebx                  ; ebx = keyLen
    xor edx, edx                  ; edx = uniqueKey
    movzx esi, lastAlgo

    mov edi, OFFSET seen256
    mov ecx, 256
    xor al, al
    rep stosb

    cmp esi, 1           
    je  SM_CAESAR
    cmp esi, 2           
    je  SM_XOR
    cmp esi, 3           
    je  SM_SUB
    cmp esi, 4           
    je  SM_OTP
    jmp SM_AFTER_KEY

SM_CAESAR:
    mov eax, 10
    mov ebx, 1
    movzx ecx, lastCaesarKey
    mov edi, OFFSET seen256
    mov BYTE PTR [edi+ecx], 1
    mov edx, 1
    jmp SM_AFTER_KEY

SM_XOR:
    mov eax, 15
    mov ebx, 1
    movzx ecx, lastXORKey
    mov edi, OFFSET seen256
    mov BYTE PTR [edi+ecx], 1
    mov edx, 1
    jmp SM_AFTER_KEY

SM_SUB:
    mov eax, 30
    mov ebx, 26
    mov ecx, 26
    mov esi, OFFSET subTable
    mov edi, OFFSET seen256
SM_SUB_L:
    mov al, [esi]
    cmp BYTE PTR [edi+eax], 0
    jne SM_SUB_N
    mov BYTE PTR [edi+eax], 1
    inc edx                   
SM_SUB_N:
    inc esi
    loop SM_SUB_L
    jmp SM_AFTER_KEY

SM_OTP:
    mov eax, 90
    mov ebx, [len]           
    mov ecx, ebx
    jecxz SM_AFTER_KEY
    mov esi, OFFSET keyOTP
    mov edi, OFFSET seen256
SM_OTP_L:
    mov al, [esi]
    cmp BYTE PTR [edi+eax], 0
    jne SM_OTP_N
    mov BYTE PTR [edi+eax], 1
    inc edx
SM_OTP_N:
    inc esi
    loop SM_OTP_L

SM_AFTER_KEY:
  
    mov edi, OFFSET seen256     
    mov ecx, 256
    xor al, al
    rep stosb                   

    xor ecx, ecx                
    mov esi, OFFSET cipher
    mov ebp, [len]
    test ebp, ebp              
    jz  SM_AFTER_MSG
SM_MSG_L:
    mov bl, [esi]               
    cmp BYTE PTR [edi+ebx], 0
    jne SM_MSG_N
    mov BYTE PTR [edi+ebx], 1
    inc ecx
SM_MSG_N:
    inc esi
    dec ebp
    jnz SM_MSG_L
SM_AFTER_MSG:
  
    mov esi, ebx
    cmp esi, 32
    jbe SM_KL_OK
    mov esi, 32
SM_KL_OK:
    shl esi, 1

    ; diversity score
    mov edi, edx
    add edi, ecx
    cmp edi, 64
    jbe SM_DIV_OK
    mov edi, 64
SM_DIV_OK:

    ; total score
    mov ebp, eax
    add ebp, esi
    add ebp, edi
    cmp ebp, 100
    jbe SM_TOK
    mov ebp, 100
SM_TOK:

    ; rating only
    mov eax, ebp
    cmp eax, 40
    jb  SM_W
    cmp eax, 75
    jb  SM_M
    mov edx, OFFSET smStrong
    call WriteString
    call Crlf
    jmp SM_DONE
SM_M:
    mov edx, OFFSET smMedium
    call WriteString
    call Crlf
    jmp SM_DONE
SM_W:
    mov edx, OFFSET smWeak
    call WriteString
    call Crlf

SM_DONE:
    popad
    ret
StrengthMeter ENDP


ReadFileToBuffer PROC
    ; ask filename
    mov edx, OFFSET fileReadMsg
    call WriteString
    mov edx, OFFSET inFileName
    mov ecx, 80
    call ReadString

    ; Open file
    mov edx, OFFSET inFileName
    call OpenInputFile 
    cmp eax, INVALID_HANDLE_VALUE
    je FileOpenFail
    mov fileHandle, eax

    ; Read file into 'text'
    mov eax, fileHandle
    mov edx, OFFSET text
    mov ecx, 255
    call ReadFromFile

    cmp eax, 0
    je FileReadFail

    mov [len], eax   ; store bytes read

    ; close file
    mov eax, fileHandle
    call CloseFile
    ret

FileOpenFail:
    mov edx, OFFSET fileErrOpen
    call WriteString
    call CrLf
    call crlf
    mov FOF,0
    ret

FileReadFail:
    mov edx, OFFSET fileErrRead
    call WriteString
    call CrLf
    call crlf
    mov FRF,0
    ret
ReadFileToBuffer ENDP

WriteBufferToFile PROC
    ; ask filename
    mov edx, OFFSET fileWriteMsg
    call WriteString
    mov edx, OFFSET outFileName
    mov ecx, 80
    call ReadString

    ; Create output file
    mov edx, OFFSET outFileName
    call CreateOutputFile    
    mov fileHandle, eax

    ; Write buffer
    mov eax, fileHandle
    mov ecx, [len]       
    call WriteToFile

    ; Close file
    mov eax, fileHandle
    call CloseFile

    mov edx, OFFSET fileSaved
    call WriteString
    call CrLf
    ret
WriteBufferToFile ENDP


ResultEnc PROC
    call Clrscr
    mov edx,offset summaryBoxStar 
    call writeString
    mov edx, offset encTEXT
	call WriteString
	mov edx, offset cipher
	call WriteString
    call PrintSummary
    call StrengthMeter
    mov edx,offset footer
    call writeString
    call crlf
    ret

ResultEnc ENDP

ResultDec PROC
    call Clrscr
    mov edx, offset decTEXT
	call WriteString
	mov edx, offset decrypted
	call WriteString
	call crlf
    call crlf
    ret

ResultDec ENDP

;--------------- MAIN ------------------------
 MAIN PROC

     call IntroAnimation 

	LOOP_START:
   
	call printMENU 
	call crlf

	mov edx,offset optMSGTOP 
	call writeString
    mov edx,offset optMSGBody 
	call writeString
    mov edx,offset otpDecor
    call writeString
    mov edx,offset optMSGBottom 
	call writeString
	
	
    
	call readINT     ; choice input
	mov opt,al  


	cmp opt,1
	je encrypt_text
	cmp opt,2
	je decrypt_text
    cmp opt, 3
    je do_file_encrypt
	cmp opt,4
	je do_freq_analysis
	cmp opt,5
	je done

jmp LOOP_START

	encrypt_text:

	mov edx,offset prompt ; input msg
	call writeString
	

	call takeInput
	mov [len], eax
	mov ecx,eax ; readstring jo length return karega eax mein usko ecx mein store krdia
	call Clrscr

	

	call printAlgo

	call readINT   ; taking choice from user
	mov choice,al

	cmp choice,1
	je do_encryptCeaser
	cmp choice,2
	je do_encryptXOR
	cmp choice,3
	je do_encryptSUB
	cmp choice,4
	je do_encryptOTP
	
	jmp LOOP_START

do_encryptCeaser:
	call EncryptCaesar
	jmp LOOP_START

do_encryptXOR:
	call encryptXOR
	jmp LOOP_START

do_encryptSUB:
	push ecx
	call encryptSUB
	jmp LOOP_START
	
do_encryptOTP:
	push ecx
	call encryptOTP
	jmp LOOP_START

do_freq_analysis:
    call FrequencyAnalysis
    jmp LOOP_START




decrypt_text:
    call clrscr
    cmp isEncrypted, 1        ; check if encryption was done
    jne noCipherAvailable

	
	call printAlgo

	call readINT
	mov choice,al
	mov bl,lastAlgo
	cmp bl, al
	jne algoMismatch

	cmp choice,1
	je do_decryptCaesar
	cmp choice,2
	je do_decryptXOR
	cmp choice,3
	je do_decryptSUB
	cmp choice,4
	je do_decryptOTP
	
	jmp LOOP_START

do_decryptCaesar:
	call DecryptCaesar
	jmp LOOP_START
do_decryptXOR:
	call decryptXOR
	jmp LOOP_START

do_decryptSUB:
	call decryptSUB
	jmp LOOP_START

do_decryptOTP:
	call decryptOTP
	jmp LOOP_START

	noCipherAvailable:
    mov edx, OFFSET MSG_NO_CIPHER
    call WriteString
    call crlf
    call crlf
    jmp LOOP_START

	algoMismatch:
    mov edx, OFFSET MSG_ALGO_MISMATCH
    call WriteString
    call crlf
    jmp LOOP_START

    do_file_encrypt:
    call ReadFileToBuffer        ; load file ? text[]
    mov dl, FOF
    cmp dl, 0
    je LOOP_START                ; jump to menu if file open failed
    
    mov dl, FRF
    cmp dl, 0
    je LOOP_START 
    
    call printAlgo
    call ReadInt
    mov choice, al

    cmp choice, 1
    je file_do_caesar
    cmp choice, 2
    je file_do_xor
    cmp choice, 3
    je file_do_sub
    cmp choice, 4
    je file_do_otp
    jmp LOOP_START

file_do_caesar:
    call EncryptCaesar
    mov edx, OFFSET cipher
    call WriteBufferToFile
    jmp LOOP_START

file_do_xor:
    call EncryptXor
    mov edx, OFFSET cipher
    call WriteBufferToFile
    jmp LOOP_START

file_do_sub:
    push len
    call encryptSub
    mov edx, OFFSET cipher
    call WriteBufferToFile
    jmp LOOP_START

file_do_otp:
    push len
    call encryptOTP
    mov edx, OFFSET cipher
    call WriteBufferToFile
    jmp LOOP_START

	done:
	mov edx,offset exitMsg
    call writeString
    call crlf

EXIT
  MAIN ENDP
  END MAIN
